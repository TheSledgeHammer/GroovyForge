buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		gradlePluginPortal()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.matthewprenger.cursegradle'

File mavenGradle = file('maven.gradle')
mavenGradle = file(mavenGradle.absolutePath)
apply from: mavenGradle

def build_number = String.valueOf(getProperty("build_number"))
version = "${version_major}.${version_minor}.${version_patch}"
group = "ooo.thesledgehammer"
archivesBaseName = "groovyforge"

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

repositories {
	maven {
		name = "Apache Groovy"
		url = "https://mvnrepository.com/artifact/org.codehaus.groovy/groovy"
	}
}

minecraft {
	mappings channel: "${mcp_channel}", version:"${mcp_version}"

	runs {
		client {
			workingDirectory project.file('Minecraft')

			properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'

			// Recommended logging level for the console
			property 'forge.logging.console.level', 'info'
			property "mixin.debug", "true"

			mods {
				"groovyforge" {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run-server')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'info'

			mods {
				"groovyforge" {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run-server')
			property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			forceExit false
			//args '--mod', 'groovyforge', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
		}
	}
}

// Include resources generated by data generators.
//sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
	minecraft "net.minecraftforge:forge:${mcversion}-${forgeversion}"
	implementation  "org.codehaus.groovy:groovy-all:${groovy_version}"
}

jar {
	manifest {
		attributes([
				'FMLModType': 'LANGPROVIDER'
		])
		attributes([
				'Specification-Title': project.name,
				'Specification-Vendor': "TheSledgeHammer",
				'Specification-Version': "1",
				'Implementation-Title': project.name,
				'Implementation-Version': "5",
				'Implementation-Vendor' : "TheSledgeHammer",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

curseforge {
	if (project.hasProperty('curseforge_key')) {
		apiKey = project.curseforge_key
	}
	project {
		id = project.projectId
		changelog = "A changelog can be found at https://github.com/TheSledgeHammer/GroovyForge"
		changelogType = 'markdown'
		releaseType = project.curse_type
		addGameVersion project.mcversion
		mainArtifact(jar) {
			displayName = "GroovyForge"
		}
	}
}
